import org.apache.tools.ant.taskdefs.condition.Os

import java.time.LocalDate

plugins {
  id 'story-mapping.java-application-conventions'
}

dependencies {
  implementation project(':story-mapping-backend')
  implementation project(':story-mapping-frontend')
}

application {
  mainModule = 'de.muspellheim.storymapping'
  mainClass = 'de.muspellheim.storymapping.App'
}

def year = LocalDate.now().year

if (Os.isFamily(Os.FAMILY_MAC)) {
  jlink {
    jpackage {
      icon = 'src/main/macos/AppIcon.icns'
      imageName = 'Story Mapping'
      installerType = 'dmg'
      imageOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
        '--mac-sign',
        '--mac-signing-key-user-name', project.findProperty('mac.signingKeyUserName')
      ]
    }
  }
}

if (Os.isFamily(Os.FAMILY_WINDOWS)) {
  jlink {
    jpackage {
      icon = 'src/main/win/app.ico'
      imageName = 'Story Mapping'
      imageOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
      ]
      installerName = 'Story Mapping'
      installerType = 'msi'
      installerOptions = [
        '--copyright', "Copyright (c) ${year} Falko Schumann",
        '--license-file', '../LICENSE.txt',
        '--win-dir-chooser',
        '--win-menu',
        '--win-menu-group', 'Muspellheim',
        '--win-upgrade-uuid', project.findProperty('win.upgradeUuid')
      ]
    }
  }
}
